plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}

repositories {
  mavenLocal()
  mavenCentral()
}

group = "de.wellnerbou"
sourceCompatibility = JavaVersion.VERSION_1_8
archivesBaseName = "java-grok"

jar {
    baseName = project.name
    version = project.version
}

dependencies {
  compile "org.apache.commons:commons-lang3:3.7"
  compile "org.slf4j:slf4j-api:1.7.21"

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
  testCompile "com.google.guava:guava:24.0-jre"
}


// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'java-grok'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'java-grok'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://gitlab.com/paulwellnerbou/java-grok.git'
    }
}
